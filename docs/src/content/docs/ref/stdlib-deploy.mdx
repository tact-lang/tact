---
title: "@stdlib/deploy"
description: "Provides unified mechanisms for deployments"
---

import { Badge } from '@astrojs/starlight/components';

Provides unified mechanisms for deployments.

To use this library, import `@stdlib/deploy`:

```tact
import "@stdlib/deploy";
```

## Functions

### cashback

<Badge text="Gas-expensive" title="Uses 500 gas units or more" variant="danger" size="medium"/>
<Badge text="Available since Tact 1.6.1" variant="tip" size="medium"/><p/>

```tact
fun cashback(to: Address);
```

[Queues](/book/send#outbound-message-processing) an empty message to be sent with the [`SendRemainingValue{:tact}`](/book/message-mode#base-modes) mode to the destination [`Address{:tact}`](/book/types#primitive-types) `to`. This is the most gas-efficient way to send the remaining value from the incoming message to the given address.

Attempts to queue more than $255$ messages throw an exception with [exit code 33](/book/exit-codes#33): `Action list is too long`.

Usage example:

```tact
// Forward the remaining value back to the sender
cashback(sender());
```

## Traits

### EmptyDeployable

<Badge text="Available since Tact 1.6.1" variant="tip" size="medium"/><p/>

The trait `EmptyDeployable{:tact}` provides a simple unified mechanism for deployments by adding an empty `receive()` function, which handles all incoming messages with an empty body, i.e., `null{:tact}` body.

To deploy a contract with this trait, send a message with [initial code and data](/book/expressions#initof) and an empty message body.

Source code:

```tact
trait EmptyDeployable {
    /// `null` message body receiver for deployment purposes.
    receive() { }
}
```

Usage example:

```tact /EmptyDeployable/
import "@stdlib/deploy";

contract ExampleContract with EmptyDeployable {
    // Now, this contract has a `null` message body receiver,
    // which is convenient for most deployments
}
```

### CashbackDeployable

<Badge text="Available since Tact 1.6.1" variant="tip" size="medium"/><p/>

Similar to [`EmptyDeployable{:tact}`](#emptydeployable) trait, but also forwards the remaining incoming message value back to the sender contract — the initiator of the deployment.

Source code:

```tact
trait CashbackDeployable {
    /// `null` message body receiver for deployment purposes.
    /// Forwards the remaining value back to the sender.
    receive() { cashback(sender()) }
}
```

Usage example:

```tact /CashbackDeployable/
import "@stdlib/deploy";

contract ExampleContract with CashbackDeployable {
    // Now, this contract has a `null` message body receiver,
    // which will forward the remaining value in the message
    // back to the sender contract — the initiator of the deployment
}
```

### Deployable

<Badge text="Deprecated since Tact 1.6" variant="tip" size="medium"/><p/>

The trait `Deployable{:tact}` provides a unified mechanism for deployments by implementing a simple receiver for the [`Deploy{:tact}`](#deploy) message.

All contracts are deployed by sending them a message. While any message can be used for this purpose, you can use the special [`Deploy{:tact}`](#deploy) message.

This message has a single field, `queryId`, provided by the deployer (usually set to zero). If the deployment succeeds, the contract will reply with a [`DeployOk{:tact}`](#deployok) message and echo the same `queryId` in the response.

Source code:

```tact
trait Deployable {
    receive(deploy: Deploy) {
        self.notify(DeployOk{queryId: deploy.queryId}.toCell());
    }
}
```

Usage example:

```tact /Deployable/
import "@stdlib/deploy";

contract ExampleContract with Deployable {
    // Now, this contract has a receiver for the Deploy message
}
```

### FactoryDeployable

<Badge text="Deprecated since Tact 1.6" variant="tip" size="medium"/><p/>

The trait `FactoryDeployable{:tact}` provides a convenient unified mechanism for chained deployments.

Source code:

```tact
trait FactoryDeployable {
    receive(deploy: FactoryDeploy) {
        self.forward(deploy.cashback, DeployOk{queryId: deploy.queryId}.toCell(), false, null);
    }
}
```

Usage example:

```tact /FactoryDeployable/
import "@stdlib/deploy";

contract ExampleContract with FactoryDeployable {
    // Now, this contract has a receiver for the FactoryDeploy message
}
```

## Messages

### Deploy

Message struct used in a receiver of the **deprecated** [`Deployable{:tact}`](#deployable) trait.

```tact
message Deploy {
    /// Unique identifier for tracking transactions across multiple contracts.
    queryId: Int as uint64;
}
```

### DeployOk

Forwarded message struct used in **deprecated** [`Deployable{:tact}`](#deployable) and [`FactoryDeployable{:tact}`](#factorydeployable) traits.

```tact
message DeployOk {
    /// Unique identifier for tracking transactions across multiple contracts.
    queryId: Int as uint64;
}
```

### FactoryDeploy

Message struct used in a receiver of the **deprecated** [`FactoryDeployable{:tact}`](#factorydeployable) trait.

```tact
message FactoryDeploy {
    /// Unique identifier for tracking transactions across multiple contracts.
    queryId: Int as uint64;

    /// Address to forward cashback to.
    cashback: Address;
}
```

## Sources

* [deploy.tact](https://github.com/tact-lang/tact/blob/61541b7783098e1af669faccd7d2334c10981c72/stdlib/libs/deploy.tact)
