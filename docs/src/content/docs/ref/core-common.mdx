---
title: Common
description: "Commonly used global static functions from the Core library of Tact"
---

import { Badge } from '@astrojs/starlight/components';

List of the most commonly used built-in [global static functions](/book/functions#global-static-functions).

## Contextual

### now

```tact
fun now(): Int;
```

Returns the current [Unix time](https://en.wikipedia.org/wiki/Unix_time).

Usage example:

```tact
let timeOffset: Int = now() + 1000; // thousand seconds from now()
```

### myBalance

```tact
fun myBalance(): Int;
```

Returns the [nanoToncoin](/book/integers#nanotoncoin) [`Int{:tact}`][int] balance of the smart contract as it was at the start of the [compute phase](https://docs.ton.org/learn/tvm-instructions/tvm-overview#compute-phase) of the current transaction.

Usage example:

```tact
let iNeedADolla: Int = myBalance();
```

:::caution

  Beware that [all message-sending functions](/book/send#message-sending-functions) of Tact can change the _actual_ balance of the contract, but they **won't** update the value returned by this function.

:::

### myAddress

```tact
fun myAddress(): Address;
```

Returns the address of the current smart contract as an [`Address{:tact}`][p].

Usage example:

```tact
let meMyselfAndI: Address = myAddress();
```

### sender

```tact
fun sender(): Address;
```

Returns the [`Address{:tact}`][p] of the sender of the current message.

Usage example:

```tact
contract MeSee {
    receive() {
        let whoSentMeMessage: Address = sender();
    }
}
```

:::caution

  Behavior is undefined for [getter functions](/book/contracts#getter-functions), because they cannot have a sender nor can they send messages.

:::

:::note

  In order to reduce gas usage, prefer using this function over calling [`context().sender{:tact}`](#context) when you only need to know the sender of the message.

:::

### context

```tact
fun context(): Context;
```

Returns `Context{:tact}` [struct](/book/structs-and-messages#structs), which consists of:

Field        | Type                    | Description
:----------- | :---------------------- | :----------
`bounceable` | [`Bool{:tact}`][bool]   | Indicates whether the received message can [bounce back](https://docs.ton.org/v3/documentation/smart-contracts/message-management/non-bounceable-messages).
`sender`     | [`Address{:tact}`][p]   | Internal address of the sender on the TON blockchain.
`value`      | [`Int{:tact}`][int]     | Amount of [nanoToncoins](/book/integers#nanotoncoin) in the received message.
`raw`        | [`Slice{:tact}`][slice] | The remainder of the received message as a [`Slice{:tact}`][slice]. It follows the [internal message layout](https://docs.ton.org/develop/smart-contracts/messages#message-layout) of TON, starting from the destination [`Address{:tact}`][p] (`MsgAddressInt` in [TL-B notation](https://docs.ton.org/develop/data-formats/tl-b-language)).

Usage example:

```tact
let ctx: Context = context();
require(ctx.value != 68 + 1, "Invalid amount of nanoToncoins, bye!");
```

:::note

  If you only need to know who sent the message, use the [`sender(){:tact}`](#sender) function, as it is less gas-consuming.

:::

## Addressing

### newAddress

<Badge text="Gas-expensive" title="Uses 500 gas units or more" variant="danger" size="medium"/><p/>

```tact
fun newAddress(chain: Int, hash: Int): Address;
```

Creates a new [`Address{:tact}`][p] based on the [`chain` ID][workchain-id] and the [SHA-256](/ref/core-math#sha256) encoded [`hash` value (account ID)][account-id].

This function tries to resolve constant values at [compile-time](/ref/core-comptime) whenever possible.

Attempts to specify an invalid `chain` ID (not $-1$ or $0$) detectable at [compile-time](/ref/core-comptime) will result in a compilation error.

Usage example:

```tact
let oldTonFoundationAddr: Address =
    newAddress(0, 0x83dfd552e63729b472fcbcc8c45ebcc6691702558b68ec7527e1ba403a0f31a8);
    //         ↑  ↑
    //         |  SHA-256 hash of contract's init package (StateInit)
    //         chain ID: 0 is a workchain, -1 is a masterchain
```

:::caution

  Make sure you specify only supported workchain IDs: $0$ for the basechain and $-1$ for the masterchain.

:::

:::note[Useful links:]

  [`chain` (Workchain ID) in TON Docs][workchain-id]\
  [`hash` (Account ID) in TON Docs][account-id]\
  [Contract's init package (`StateInit{:tact}`)](/book/expressions#initof)

:::

### hasSameBasechainAddress

<Badge text="Available since Tact 1.6.1" variant="tip" size="medium"/><p/>

```tact
extends fun hasSameBasechainAddress(self: StateInit, sender: Address): Bool;
```

Efficiently compares whether the given address matches the basechain address of the contract. Returns true if the addresses are the same; false otherwise.

```tact
fun example() {
    let init = initOf SomeContract();
    init.hasSameBasechainAddress(sender()); // returns true if sender matches contract's basechain address
}
```

This function provides a gas-optimized implementation compared to direct address comparison:

```tact
fun example() {
    let init = initOf SomeContract();
    sender() == contractAddress(sender()); // less efficient approach
}
```

:::note
  This function works only for basechain addresses!
:::

### contractAddress

<Badge text="Gas-expensive" title="Uses 500 gas units or more" variant="danger" size="medium"/><p/>

```tact
fun contractAddress(s: StateInit): Address;
```

Computes the smart contract's [`Address{:tact}`][p] in the workchain ID $0$ (basechain) using the [`StateInit{:tact}`](/book/expressions#initof) `s` of the contract. An alias to `contractAddressExt(0, s.code, s.data){:tact}`.

Usage example:

```tact
let s: StateInit = initOf SomeContract();
let foundMeSome: Address = contractAddress(s);
let andSomeMore: Address = contractAddressExt(0, s.code, s.data);

foundMeSome == andSomeMore; // true
```

### contractAddressExt

<Badge text="Gas-expensive" title="Uses 500 gas units or more" variant="danger" size="medium"/><p/>

```tact
fun contractAddressExt(chain: Int, code: Cell, data: Cell): Address;
```

Computes the smart contract's [`Address{:tact}`][p] in the given `chain` ID using the contract's `code` and its initial state `data`. Use the [`initOf{:tact}`](/book/expressions#initof) expression to obtain the initial `code` and initial `data` of a given contract.

This function tries to resolve constant values at [compile-time](/ref/core-comptime) whenever possible.

Attempts to specify an invalid `chain` ID (not $-1$ or $0$) that can be detected at [compile-time](/ref/core-comptime) will result in a compilation error.

Usage example:

```tact
let initPkg: StateInit = initOf SomeContract();
let hereBeDragons: Address = contractAddressExt(0, initPkg.code, initPkg.data);
```

### contractHash

<Badge text="Available since Tact 1.6" variant="tip" size="medium"/><p/>

```tact
fun contractHash(code: Cell, data: Cell): Int;
```

Computes and returns an [`Int{:tact}`][int] value of the [SHA-256](https://en.wikipedia.org/wiki/SHA-2#Hash_standard) hash of the `code` and `data` of the given contract. To assemble the `code` and `data` cells together for hashing, the [standard `Cell{:tact}` representation](/book/cells#cells-representation) is used.

This hash is commonly called the [account ID][account-id]. Together with the [workchain ID][workchain-id], it deterministically forms the address of the contract on TON Blockchain.

Usage example:

```tact
let initPkg: StateInit = initOf SomeContract();
let accountId: Int = contractHash(initPkg.code, initPkg.data);
let basechainAddr: Address = newAddress(0, accountId);
let basechainAddr2: Address = contractAddressExt(0, initPkg.code, initPkg.data);

basechainAddr == basechainAddr2; // true
```

:::note[Useful links:]

  [`chain` (Workchain ID) in TON Docs][workchain-id]\
  [`hash` (Account ID) in TON Docs][account-id]\
  [Contract's init package (`StateInit{:tact}`)](/book/expressions#initof)\
  [Standard `Cell{:tact}` representation](/book/cells#cells-representation)

:::

## Communication

### send

<Badge text="Gas-expensive" title="Uses 500 gas units or more" variant="danger" size="medium"/><p/>

```tact
fun send(params: SendParameters);
```

[Queues the message](/book/send#outbound-message-processing) to be sent using a [`SendParameters{:tact}`](/book/send) [Struct][struct].

Attempts to queue more than $255$ messages throw an exception with [exit code 33](/book/exit-codes#33): `Action list is too long`.

Usage example:

```tact
send(SendParameters{
    to: sender(),    // back to the sender,
    value: ton("1"), // with 1 Toncoin (1_000_000_000 nanoToncoin),
                     // and no message body
});
```

:::note[Useful links:]

  [Sending messages in the Book](/book/send)\
  [Message `mode` in the Book](/book/message-mode)\
  [Single-contract communication in the Cookbook](/cookbook/single-communication)

:::

### message

<Badge text="Gas-expensive" title="Uses 500 gas units or more" variant="danger" size="medium"/>
<Badge text="Available since Tact 1.6" variant="tip" size="medium"/><p/>

```tact
fun message(params: MessageParameters);
```

[Queues the message](/book/send#outbound-message-processing) to be sent using the `MessageParameters{:tact}` [struct][struct]. Allows for cheaper non-deployment regular messages compared to the [`send(){:tact}`](#send) function.

The `MessageParameters{:tact}` [struct][struct] is similar to the [`SendParameters{:tact}`](/book/send) [struct][struct], but without the `body` and `code` fields.

Attempts to queue more than $255$ messages throw an exception with an [exit code 33](/book/exit-codes#33): `Action list is too long`.

Usage example:

```tact
message(MessageParameters{
    to: sender(),    // back to the sender,
    value: ton("1"), // with 1 Toncoin (1_000_000_000 nanoToncoin),
                     // and no message body
});
```

:::note[Useful links:]

  [Sending messages in the Book](/book/send)\
  [Message `mode` in the Book](/book/message-mode)

:::

### deploy

<Badge text="Gas-expensive" title="Uses 500 gas units or more" variant="danger" size="medium"/>
<Badge text="Available since Tact 1.6" variant="tip" size="medium"/><p/>

```tact
fun deploy(params: DeployParameters);
```

[Queues](/book/send#outbound-message-processing) the [contract deployment message](/book/deploy) to be sent using the `DeployParameters{:tact}` [struct][struct]. Allows for cheaper on-chain deployments compared to the [`send(){:tact}`](#send) function.

The `DeployParameters{:tact}` [struct][struct] consists of the following fields:

Field    | Type                          | Description
:------- | :---------------------------- | :----------
`mode`   | [`Int{:tact}`][int]           | An 8-bit value that configures how to send a message, defaults to $0$. See: [Message `mode`](/book/message-mode).
`body`   | [`Cell?{:tact}`][cell]        | [Optional][opt] message body as a [`Cell{:tact}`][cell].
`value`  | [`Int{:tact}`][int]           | The amount of [nanoToncoins][nano] you want to send with the message. This value is used to cover [forward fees][fwdfee] unless the optional flag [`SendPayGasSeparately{:tact}`](/book/message-mode#optional-flags) is used.
`bounce` | [`Bool{:tact}`][p]            | When set to `true` (default), the message bounces back to the sender if the recipient contract doesn't exist or isn't able to process the message.
`init`   | [`StateInit{:tact}`][initpkg] | [Initial package][initpkg] of the contract (initial code and initial data). See: [`initOf{:tact}`][initpkg].

Attempts to queue more than $255$ messages throw an exception with an [exit code 33](/book/exit-codes#33): `Action list is too long`.

Usage example:

```tact
deploy(DeployParameters{
    init: initOf SomeContract(), // with initial code and data of SomeContract
                                 // and no additional message body
    mode: SendIgnoreErrors,      // skip the message in case of errors
    value: ton("1"),             // send 1 Toncoin (1_000_000_000 nanoToncoin)
});
```

:::note[Useful links:]

  [Sending messages in the Book](/book/send)\
  [Message `mode` in the Book](/book/message-mode)

:::

### cashback

<Badge text="Gas-expensive" title="Uses 500 gas units or more" variant="danger" size="medium"/>
<Badge text="Available since Tact 1.6.1" variant="tip" size="medium"/><p/>

```tact
fun cashback(to: Address);
```

[Queues](/book/send#outbound-message-processing) an empty message to be sent with the [`SendRemainingValue{:tact}`](/book/message-mode#base-modes) mode with the [`SendIgnoreErrors{:tact}`](/book/message-mode/#optional-flags) to the destination address `to`. It is the most gas-efficient way to send the remaining value from the incoming message to the given address.

Attempts to queue more than 255 messages throw an exception with [exit code 33](/book/exit-codes#33): `Action list is too long`.

Usage examples:

```tact
// Forward the remaining value back to the sender
cashback(sender());

// The cashback() function above is cheaper, but functionally
// equivalent to the following call to the message() function
message(MessageParameters{
    mode: SendRemainingValue | SendIgnoreErrors,
    body: null,
    value: 0,
    to: sender(),
    bounce: false,
});
```

### emit

<Badge text="Gas-expensive" title="Uses 500 gas units or more" variant="danger" size="medium"/><p/>

```tact
fun emit(body: Cell);
```

[Queues the message](/book/send#outbound-message-processing) `body` to be sent to the outer world for the purpose of logging and analyzing it later off-chain. The message does not have a recipient and is more gas-efficient compared to using any other [message-sending functions](/book/send#message-sending-functions) of Tact.

The message is sent with the default mode: [`SendDefaultMode`](/book/message-mode#base-modes) ($0$).

Attempts to queue more than $255$ messages throw an exception with an [exit code 33](/book/exit-codes#33): `Action list is too long`.

Usage example:

```tact
emit("Catch me if you can, Mr. Holmes".asComment()); // asComment() converts a String to a Cell
```

:::note

  To analyze `emit(){:tact}` calls, one must look at the [external messages](/book/external) produced by the contract.

  Read more: [Logging via `emit(){:tact}`](/book/debug#logging).

:::

[p]: /book/types#primitive-types
[bool]: /book/types#booleans
[int]: /book/integers
[cell]: /book/cells#cells
[slice]: /book/cells#slices
[struct]: /book/structs-and-messages#structs
[opt]: /book/optionals

[nano]: /book/integers#nanotoncoin
[initpkg]: /book/expressions#initof

[fwdfee]: https://docs.ton.org/develop/howto/fees-low-level#forward-fees
[workchain-id]: https://docs.ton.org/learn/overviews/addresses#workchain-id
[account-id]: https://docs.ton.org/learn/overviews/addresses#account-id
